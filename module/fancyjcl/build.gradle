plugins {
    id 'com.android.library'
    id 'maven-publish'
}
apply plugin: 'com.ydq.android.gradle.native-aar.import'

android {
    compileSdk 31

    defaultConfig {
        minSdk 19
        targetSdk 31
        consumerProguardFiles "consumer-rules.pro"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode 0
        versionName "0.0.1"
    }
    splits {
        abi {
            enable false
            universalApk true
        }
    }

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip '**/*.so'
            }
            jniDebuggable true
            debuggable true
            renderscriptDebuggable true
            renderscriptOptimLevel 0
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            externalNativeBuild {
                cmake {
                    cppFlags '-DBUILD_TYPE=release'
                }
            }
        }
    }
    packagingOptions {
        exclude 'lib/*/libOpenCL.so'
        pickFirst 'lib/*/libfancier.so'
        pickFirst 'lib/*/libfancier_tiling.so'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_15
        targetCompatibility JavaVersion.VERSION_15
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
}

task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompile.classpath
        }
    }
    destinationDir = file("${project.rootDir}/../docs/")
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.HPC-ULL'
            version = version
            artifactId = project.name
            afterEvaluate {
                from components.release
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.github.DoMondo:Fancier:0.0.1'
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
}